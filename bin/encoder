#!/ports/bin/perl

use Modern::Perl;

use Media;
use IO::All;
use Pod::Usage;

use constant PROCESS_ID_FILE  => "$ENV{'HOME'}/var/encoder.pid";
use constant SIG_CHECK        => 0;
use constant SIG_KILL         => 9;
use constant SIG_STOP         => 17;
use constant SIG_CONT         => 19;



my $media        = Media->new();
my $encoder      = Media->new( log_file => 'encoder.log' );
my $base         = $media->get_config( 'base_directory' );
my $auto_install = $media->get_config( 'install_encodes' );

my $argument = shift // 'queue';
given ( $argument ) {
    when ( 'run'    ) { run_encoder_queue();          }
    
    when ( 'queue'  ) { list_encoder_queue();         }
    
    when ( 'pause'  ) { pause_current_encode();       }
    when ( 'stop'   ) { stop_encoder_after_current(); }
    when ( 'abort'  ) { stop_encoder_immediately();   }
    when ( 'resume' ) { resume_current_encode();      }
    
    when ( 'remove' ) { remove_from_queue( @ARGV );   }
    when ( 'clear'  ) { remove_from_queue( '-p', '0' ); }
    when ( 'help'   ) { pod2usage();                    }
    
    default           { die "Unknown: $argument";     }
}
exit;



sub run_encoder_queue {
    # always start in the base
    chdir $base
        or die "chdir $base: $!";
    say "-> Starting in $base";
    
    # run the encoder at as low a priority as possible
    setpriority( 0, $$, 20 );
    
    $$ > io( PROCESS_ID_FILE );
    
    while ( 1 ) {
        my( $job, $data ) = $encoder->dequeue_conversion();
        my $end_queue = $data->{'end_queue'} // 0;
        
        convert_video( $data )
            unless $end_queue;
        
        $job->finish();
        
        last if $end_queue;
    }
}
sub pause_current_encode {
    my $pid < io( PROCESS_ID_FILE );
    my @children = get_child_processes( $pid );
    
    kill SIG_STOP, @children;
}
sub resume_current_encode {
    my $pid < io( PROCESS_ID_FILE );
    my @children = get_child_processes( $pid );
    
    kill SIG_CONT, @children;
}
sub list_encoder_queue {
    foreach my $job ( $media->list_all_in_queue() ) {
        my $input    = $job->{'payload'}{'input'};
        my $handler  = $job->{'active'};
        my $active   = '';
        my $priority = $job->{'priority'};
        my $title    = $job->{'payload'}{'options'}{'-t'};
        my $stop_job = $job->{'payload'}{'end_queue'};
        
        if ( defined $stop_job ) {
            say "[--] STOP ENCODER AFTER CURRENT JOB";
        }
        else {
            if ( defined $handler ) {
                $active = ( kill 0, $handler ) ? 'active' : 'inactive'; 
            }

            say "[${priority}] $input"
              . ( defined $title   ? " title $title"               : '' )
              . ( defined $handler ? " (handler $handler $active)" : '' );
            ;
        }
    }
}
sub stop_encoder_after_current {
    $media->queue_stop_job();
}
sub stop_encoder_immediately {
    my $pid < io( PROCESS_ID_FILE );
    my @children = get_child_processes( $pid );
    
    kill SIG_KILL, $pid, @children;
}
sub remove_from_queue {
    my @removals = @_;
    
    my $first           = $removals[ 0 ];
    my $by_name         = 1;
    my $lowest_priority = 2;
    
    return unless defined $first;
    
    if ( '-p' eq $first ) {
        shift @removals;
        $lowest_priority = shift @removals;
        
        if ( ! @removals ) {
            $by_name = 0;
            unshift @removals, '';
        }
    }
    
    foreach my $job ( $media->list_all_in_queue() ) {
        my $input = $job->{'payload'}{'input'};
        my $title = $job->{'payload'}{'options'}{'-t'};
        my $prio  = $job->{'priority'};
        my $path  = $job->{'path'};
        
        $input = "$input title $title"
            if defined $title;
        
        foreach my $arg ( @removals ) {
            my $remove_job = 0;
            
            if ( $by_name ) {
                $remove_job = $input =~ m{${arg}}
                              &&
                              $prio > $lowest_priority;
            }
            else {
                $remove_job = $prio > $lowest_priority;
            }

            $media->remove_queued_job( $path )
                if $remove_job;
        }
    }
}

sub get_child_processes {
    my $pid    = shift;
    my $signal = shift;
    
    my $process_list = io( 'ps ax -o pid,ppid' )->pipe;
    my @processes;
    
    while ( my $line = $process_list->getline ) {
        if ( $line =~ m{ (\d+) \s (\d+) }x ) {
            push @processes, $1
                if $pid == $2;
        }
    }
    
    return @processes;
}
sub convert_video {
    my $data = shift;
    
    my $input  = $data->{'input'};
    my $output = $data->{'output'};
    
    if ( -f $input or -d $input ) {
        $encoder->convert_file( $data );

        if ( $auto_install ) {
            # reprocess directory now that it contains the
            # converted video (run as separate process)
            say "-> process $output";
            $media->process_directory( $output );
        }
    }
    else {
        # it is not an error if the item has since been removed
        say "-> $input has since vanished";
    }
}

__END__

=head1 ARGUMENTS

=over

=item B<encoder queue>

List items in the encoder queue. This is the default action, if no arguments
are provided.

=item B<encoder run>

Run a process that continually polls the queue and encodes the next job with
the highest priority.

=item B<encoder pause>

Pauses any encoder processes currently running.

=item B<encoder resume>

Resumes any encoder processes currently paused.

=item B<encoder stop>

Sends a control message to the encoder that causes it to exit, but only after
the current encode has finished processing.

=item B<encoder abort>

Exits the current encoder process immediately, even if an encode is currently
processing.

=item B<encoder remove C<regexp>>

Removes any job from the queue that matches the perl regular expression
C<regexp>. The expression is not anchored, so C<encoder remove Star> would
remove jobs in the queue that matched all of F<Stargate>, F<Star Wars> and
F<Dancing with the Stars>. It is case sensitive, so C<encoder remove star>
would not match any of the previous examples.

=item B<encoder clear>

Removes all queued jobs.

=back

=head1 PRIORITY

Each job in the queue has a priority, a numeric value between 2 and 100 (1 is
reserved for control messages). The lower the number, the higher the priority.
Jobs are queued by default with a priority of 50.

=head1 SEE ALSO

Run C<perldoc Media> to read more.
